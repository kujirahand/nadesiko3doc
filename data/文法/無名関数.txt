●無名関数/「＊＊には」構文について

無名関数とは、名前のない関数です。関数の引数に関数を指定したい場合など、手軽に関数を使いたいときに利用できる便利な機能です。

●「…には」構文 / 暗黙的な無名関数の指定

無名関数を使いたい場面というのは、関数の引数などにカスタム処理を与えたい場合です。そこで、『…には ... ここまで』構文が便利です。...の部分に書いた処理が、関数…の第一引数に与えられます。

{{{
[書式] 基本的な記述方法
(関数)には
 　　# ここに関数の第一引数に与える処理
ここまで。
---
[書式] 引数が必要な場合
(関数)には(引数)
  # ここに関数の第一引数に与える処理
ここまで。
---
[書式] v1互換のイベント記述方法 (v3/0.1.9より)
(関数)は〜
  # ここに関数の第一引数に与える処理
ここまで
}}}

例えば、『[[plugin_system/秒後]]』命令を使って、3秒後に挨拶を表示するプログラムは、以下のように指定できます。

{{{#nako3
「(説明)3秒後に挨拶を表示します」と表示。
3秒後には
  「こんにちは」と表示。
ここまで。
}}}

ボタンを作成して、ボタンのイベントを設定するのに使えます。

{{{#nako3
挨拶ボタン＝「挨拶」のボタン作成。
挨拶ボタンをクリックした時には
　　「こんにちは！」と言う。
ここまで。
}}}

また、配列カスタムソートでは、配列変数を任意の並び順でソートできます。

{{{#nako3
A=[1,10,100]
Aを配列カスタムソートするには、(a, b)
　それ=b-a
ここまで
Aを表示。
}}}

●明示的な無名関数の指定

{{{
[書式]
F = 関数(a, b)
  # ここに無名関数の処理
ここまで
--- あるいは ---
F = ●(a, b)
  # ここに無名関数の処理
ここまで
}}}

▲足し算を行う無名関数を定義する例

{{{#nako3
F = 関数(a, b) それはa+b。ここまで。
F(3,5)を表示。
}}}

▲前置詞{関数}を付けて関数オブジェクトを明示的に指定する場合

前置詞{関数}を付けて関数オブジェクトを明示的に指定できます。

{{{#nako3
●AAAとは
　　30を戻す。
ここまで
{関数}AAAを実行して表示。
}}}

- (参考) [[#146:https://github.com/kujirahand/nadesiko3core/pull/146]]

