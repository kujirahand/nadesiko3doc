■ 拡張プラグインやソースの分割について

なでしこ3には、拡張プラグインの仕組みがあります。拡張プラグインには、JavaScriptで書かれた「jsプラグイン」と、なでしこ3自身で書かれた「nako3プラグイン」の2種類があります。

●プラグインを取り込む方法

拡張プラグインを使うには、以下のように『!「プラグイン名」を取り込む』と記述します。

{{{
# 宣言
!「プラグインのファイル名」を取り込む

#
# ここにプラグインを使ったプログラム
#
}}}

なお、v3.3以降のバージョンでは、プラグイン名を指定する際、ファイルの拡張子を省略できなくなりました。必ず「abc.mjs」「abc.js」「abc.nako3」のように拡張子を指定する必要があります。

●nako3プラグインの作り方

なでしこ3で[[関数を定義:文法/関数]]してください。すると、その関数やグローバル変数をメインプログラムで利用できます。

以下はnako3プラグインの作成例です。「sub.nako3」という名前で保存しておきます。

{{{
# ファイル: sub.nako3
●AAAとは
　　1を戻す
ここまで。

●BBBとは
　　2を戻す
ここまで。
}}}

「sub.nako3」を使うプログラムです。

{{{
!「sub.nako3」を取り込む
3回
　　AAAを表示。# 結果: 「1」が表示される
ここまで。
}}}

▲現在実行しているのは、プラグインかメインプログラムかを判定

自作nako3プラグインを作る時、プラグイン内にはメインプログラムとプラグインのどちらを実行しているかを確認できます。拡張子「.nako3」のプラグインを取り込んだ時には、『[[プラグイン名:plugin_system/プラグイン名]]』がプラグインのパスとなります。

▲プラグインスコープについて

なでしこv3.3.5より、プラグインにスコープが利用可能になりました。

プラグインスコープ(ファイルスコープ)では、「{ファイル名}__{変数名}」のようにして、プラグインスコープの変数にアクセスできます。

例えば、次のように「CAFE1.nako3」「CAFE2.nako3」という二つのファイルを配置します。

{{{
# file: CAFE1.nako3
朝食値段＝900
}}}
{{{
# file: CAFE2.nako3
朝食値段＝700
}}}

そして、メインプログラムから上記のファイルを取り込みます。

{{{
# file: main.nako3
!「CAFE1.nako3」を取り込む
!「CAFE2.nako3」を取り込む
CAFE1__朝食値段を表示。# 900
CAFE2__朝食値段を表示。# 700
}}}

なお、「{ファイル名}__{関数名}」のように指定するのですが、ファイル名はパスや拡張子を削ったものです。例えば「a/b/c/ddd.nako3」であれば、ファイル名は「ddd」となります。


●jsプラグインの作り方

JavaScriptでモジュールを作ります。
詳しい作り方は、[[こちら:FAQ/plugins]]をご覧ください。






