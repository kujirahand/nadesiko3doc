● 詳しい解説

無名関数FでDOMに対してマウスボタンを押した時に実行するイベントを設定します。


▲簡単な利用例

なでしこエディタでは、自動的に変数「描画中キャンバス」が設定されます。これに対して、「マウス押した時」を設定することで、簡単な描画プログラムも作れます。

以下、赤色の四角をクリックすると、白色の円を描画します。

{{{#nako3(canvas,size=300x200)
赤色に塗色設定。
[0, 0, 300, 200]に四角描画。

描画中キャンバスをマウス押した時には
　　白色に塗色設定。
　　[マウスX, マウスY]へ20の円描画。
ここまで。
}}}

▲利用例　その2

描画と同時にマウスの座標も表示してみます。

{{{#nako3(rows=8,canvas,size=310x200)
アリスブルー色に塗色設定。
[0,0,310,200]へ四角描画。
「マウスX：   , マウスY：」のラベル作成して、座標確認へ代入。
黒色に塗色設定。

描画中キャンバスをマウス押した時には、
　[マウスX,マウスY]に１０の円描画。
　座標確認へ「(マウスX：{マウスX} , マウスY：{マウスY})」をテキスト設定。
ここまで。
}}}

▲『押したボタン』について

{{{#nako3
OK=「マウスイベントのテスト」のボタン作成。改行作成。
ログ＝「ログ」のテキストエリア作成。
OKのクリックした時には：
　　「クリック」と表示処理
　　押したボタンを表示処理
OKのマウス押した時には：
　　「マウス押した時」と表示処理
　　押したボタンを表示処理
OKのマウス離した時には：
　　「マウス離した時」と表示処理
OKのダブルクリックした時には：
　　「ダブルクリック」と表示処理
　　押したボタンを表示処理
OKのマウス入った時には：
　　「マウス入った時」と表示処理
OKのマウス出た時には：
　　「マウス出た時」と表示処理

●（Sの|Sと|Sを）表示処理とは：
    L=ログのテキスト取得
    L=S&改行＆L
    ログにLをテキスト設定
}}}

▲参考

- [[チュートリアル/マウス操作]]

▲関連

- [[plugin_browser/クリック時]]
- [[plugin_browser/マウス押時]]
- [[plugin_browser/マウス移動時]]
- [[plugin_browser/マウス離時]]
- [[plugin_browser/マウスX]]
- [[plugin_browser/マウスY]]
