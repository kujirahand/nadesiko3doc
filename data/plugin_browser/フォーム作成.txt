●解説

投稿フォームの一覧を簡単に作成します。第一引数には、投稿フォームの設定{method: "GET", action: "URL"}などを指定し、第二引数には入力項目の一覧を一行ずつ指定します。第二引数には、二次元配列変数を指定できます。

- [[チュートリアル/フォーム作成]] --- こちらに簡単な使い方を紹介しています。

▲利用例 - APIを呼び出すフォーム

以下は[[郵便番号API:https://api.aoikujira.com/index.php?zip]]を呼び出す入力フォームを作成する例です。

{{{#nako3
郵便番号API＝「https://api.aoikujira.com/zip/zip.php」
{action:郵便番号API}の「
fmt=xml
zn=2210824
送信=?送信
」をフォーム作成。
}}}

▲普通に入力項目をたくさん作るのに使う

たくさんの入力項目を一気に作成できます。なお入力項目のIDには「#nako3form_***」というIDが付与されるのでこの値を利用して、特定の項目だけを取り出すことが可能です。

{{{#nako3(rows=10)
L=「本が好きだ。読書が好きだ。」のラベル作成。
{}の「
color=#ffc0c0
backgroundColor=#909090
fontSize=32px
」をフォーム作成。
「スタイル適用」のボタン作成して適用ボタンに代入。
適用ボタンをクリックした時には
　　V＝「#nako3form_color」のテキスト取得。
　　Lの「color」にVをDOMスタイル設定。
　　V＝「#nako3form_fontSize」のテキスト取得。
　　Lの「fontSize」にVをDOMスタイル設定。
　　V＝「#nako3form_backgroundColor」のテキスト取得。
　　Lの「backgroundColor」にVをDOMスタイル設定。
ここまで。
}}}

v3.2.33以降で使える『フォーム入力一括取得』を使うとより便利です。

{{{#nako3(rows=10)
L=「知恵はサンゴに勝る」のラベル作成。
F={}の「
color=#ffc0c0
backgroundColor=#909090
fontSize=32px
」をフォーム作成。
OKボタン＝「スタイル適用」のボタン作成。
OKボタンをクリックした時には
    Fのフォーム入力一括取得してOBJに代入。
    LにOBJをDOMスタイル一括設定。
ここまで。
}}}

●入力オプション(v3.2.33以降)

指定できる入力オプションは以下の通り。

{{{#csv(flag=||)
オプション || 解説 || 指定例
?送信 || 送信ボタンを表示 || =?送信
?c初期値 || 色の入力ボックスを表示 || 色=?c#FF1122
?(選択肢1|選択肢2|選択肢3)初期値 ||  選択ボックスで選ぶ || 好物は=?(寿司|蕎麦|天ぷら)蕎麦
}}}

▲入力オプション(v3.6.37以降)

{{{#csv(flag=||)
オプション || 解説 || 指定例
?text=初期値=ヒント || テキスト入力 || 名前=?text=初期値=ニックネームを入力
?password=初期値=ヒント || パスワード入力 || パスワード=?password=初期値=パスワードを入力してね
?number=初期値=ヒント || 数値入力 || 番号=?number=999=番号を入力してね
?email=初期値=ヒント || Eメールの入力 || Eメール=?emal=a@example.com=メールを入力
?tel=初期値=ヒント || 電話番号の入力 || 電話=?tel=000-1111-2222=電話を入力
?color=初期値 || 色入力 || 背景色=?color=#FF0000
?hidden=値 || 不可視の値 || 秘密キー=?hidden=hoge
?checkbox=値=ラベル || チェックボックス || 卵=?checkbox=yes=卵を加える
?date=初期値 || 日付選択(年月日) || 生年月日=?date=2025/01/01
?month=初期値 || 年月選択 || 生年月=?month=2025/01
?time=初期値 || 時間選択 || 出発時間=?time=10:20
?file || ファイル入力 || ?file
}}}

利用例:

{{{#nako3
F=空の「
neko=?text=タマ=猫の名前
卵=?checkbox=yes=卵を加える
hidden=?hidden=test_hidden_key=30
生年月日=?date=2025/01/01
生年月=?month=2025/01
メール=?email==メールを入力してね
」をフォーム作成。
取得ボタン＝「取得」のボタン作成。
取得ボタンをクリックした時には
    Fのフォーム入力一括取得してOBJに代入。
    OBJをJSON_ESして表示
ここまで。
}}}

▲選択肢などの利用例

{{{#nako3
L=「知恵はサンゴに勝る」のラベル作成。
F=空の「
color=?(#ffc0c0|red|blue|white)red
backgroundColor=?c#101010
fontSize=32px
」をフォーム作成。
OKボタン＝「スタイル適用」のボタン作成。
OKボタンをクリックした時には
    Fのフォーム入力一括取得してOBJに代入。
    LにOBJをDOMスタイル一括設定。
ここまで。
}}}

●第二引数には二次元配列変数も指定可能

{{{#nako3
L=「知恵はサンゴに勝る」のラベル作成。
F=空の[
  ["color","?(#ffc0c0|red|blue|white)red"],
  ["backgroundColor","?color", "#101010"],
  ["fontSize","32px"]
]をフォーム作成。
OKボタン＝「スタイル適用」のボタン作成。
OKボタンをクリックした時には
    Fのフォーム入力一括取得してOBJに代入。
    LにOBJをDOMスタイル一括設定。
ここまで。
}}}


▲メモ

なでしこv1の[[項目記入:http://nadesi.com/man/index.php?%E9%A0%85%E7%9B%AE%E8%A8%98%E5%85%A5]]を目指して作成されています。([[開発#1122:https://github.com/kujirahand/nadesiko3/issues/1122]])

▲参考

- [[plugin_browser/フォーム入力一括取得]]



