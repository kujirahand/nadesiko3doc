●マウス操作について

ブラウザで動くなでしこ3では、ブラウザのイベントを検出して動くプログラムが作れます。

{{{
[書式]
(DOM)のマウス押した時には
　　# ここにマウスボタンが押された時の処理を記述
　　# 変数「マウスX」と「マウスY」が設定される
ここまで。
}}}

以下のように記述できます。簡単なお絵かきツールも手軽に作れます。

{{{#nako3(canvas,size=300x200)
赤色に塗色設定。
[0, 0, 300, 200]に四角描画。

描画中キャンバスをマウス押した時には
　　白色に塗色設定。
　　[マウスX, マウスY]へ20の円描画。
ここまで。
}}}

▲「マウス押した時」「マウス移動した時」「マウス離した時」を組み合わせる

「マウス押した時」と「マウス離した時」「マウス移動した時」を組み合わせると、一般的なペイントソフトのように、マウスを押している間描画するという動作を実現できます。

{{{#nako3(rows=15,canvas,size=310x200)
アリスブルー色に塗色設定。
[0,0,310,200]へ四角描画。

フラグ＝オフ
描画中キャンバスをマウス押した時には
　　フラグ＝オン
ここまで。
描画中キャンバスをマウス離した時には
　　フラグ＝オフ
ここまで。
描画中キャンバスをマウス移動した時には
　　もし、フラグがオンならば
　　　　黒色に塗り色設定。
　　　　[マウスX, マウスY]へ7の円描画。
　　ここまで。
ここまで。
}}}

▲「クリック時」と「マウス押した時」の違い

「クリック時」はボタンなどを押した時などのイベントを設定するのに利用します。マウス操作の詳しい状況を知りたい時には「マウス押した時」「マウス離した時」を使うと良いでしょう。「マウス押した時」であれば、マウスのどのボタンを押したのかの情報も得られます。

- [[plugin_browser/クリック時]] ... クリックイベントの検出に利用
- [[plugin_browser/マウス押時]] ... 詳しいマウスの情報を使いたい解きに利用

以下はイベントの使い分けを行うプログラムです。

{{{#nako3
OK=「マウスイベントのテスト」のボタン作成。改行作成。
ログ＝「ログ」のテキストエリア作成。
OKのマウス押した時には：
　　「マウス押した時」と表示処理
　　押したボタンを表示処理
OKのクリックした時には：
　　「クリック」と表示処理

●（Sの|Sと|Sを）表示処理とは：
    L=ログのテキスト取得
    L=S&改行＆L
    ログにLをテキスト設定
}}}

▲右クリックで何か処理を行いたい時

ブラウザで右クリックを使うと、大抵ブラウザのコンテキストメニューが表示されます。これを表示しないようにするには、「DOMイベント処理停止」命令を呼び出します。

以下はキャンバスをクリックするボタンで、描画する色を変更するプログラムです。左クリックで赤色を、右クリックで青色の描画を行います。

{{{#nako3(rows=15,canvas,size=310x200)
# 左クリックで赤色の円を描く
# 右クリックで青色の円を描く
青色に塗色設定。
[0, 0, 300, 200]に四角描画。
描画中キャンバスをマウス押した時には
　　# ブラウザデフォルトの動作をキャンセル
　　対象イベントのDOMイベント処理停止。
　　# 押したボタンで動作を分岐
　　もし、押したボタン＝「左」ならば
　　　　赤色に塗色設定。
　　違えば
　　　　青色に塗り色設定。
　　ここまで
　　[マウスX, マウスY]へ20の円描画。
ここまで。
描画中キャンバスを右クリックした時には
　　# ブラウザデフォルトの動作をキャンセル
　　対象イベントのDOMイベント処理停止。
ここまで。
}}}

- [[plugin_browser/DOMイベント処理停止]] ... デフォルトイベントを取り消し
- [[plugin_browser/押ボタン]] ... マウス操作で押したボタン「左」「右」「中央」
- [[plugin_browser/右クリック時]] ... 右クリックメニューのイベント


▲マウスイベントのテスト

v3.6.23以降で大量にマウスイベントが追加されました。下記でテストできます。[[#1756:https://github.com/kujirahand/nadesiko3/issues/1756]]

{{{#nako3(rows=10)
OK=「マウスイベントのテスト」のボタン作成。改行作成。
ログ＝「ログ」のテキストエリア作成。
OKのマウス押した時には：
　　「マウス押した時」と表示処理
　　押したボタンを表示処理
OKのマウス離した時には：
　　「マウス離した時」と表示処理
OKのクリックした時には：
　　「クリック」と表示処理
OKのダブルクリックした時には：
　　「ダブルクリック」と表示処理

●（Sの|Sと|Sを）表示処理とは：
    L=ログのテキスト取得
    L=S&改行＆L
    ログにLをテキスト設定
}}}

▲マウスイベントのテストその２ - マウス入った時とマウス出た時

{{{#nako3(rows=10)
OK=「マウスイベントのテスト」のボタン作成。改行作成。
OKに「イベントをログに記録するよ」をヒント設定。
ログ＝「ログ」のテキストエリア作成。
OKのマウス入った時には：
　　「マウス入った時」と表示処理
OKのマウス出た時には：
　　「マウス出た時」と表示処理

●（Sの|Sと|Sを）表示処理とは：
    L=ログのテキスト取得
    L=S&改行＆L
    ログにLをテキスト設定
}}}

▲関連する命令

- [[plugin_browser/マウス押時]]
- [[plugin_browser/マウス移動時]]
- [[plugin_browser/マウス離時]]
- [[plugin_browser/マウス入時]]
- [[plugin_browser/マウス出時]]
- [[plugin_browser/クリック時]]
- [[plugin_browser/ダブルクリック時]]
- [[plugin_browser/マウスホイール時]]

▲関連する定数

- [[plugin_browser/マウスX]]
- [[plugin_browser/マウスY]]
- [[plugin_browser/押ボタン]]
- [[plugin_browser/マウスホイール値]]

▲参考

- [[チュートリアル/キーイベント]]
- [[plugin_browser/DOM操作とイベント]]

