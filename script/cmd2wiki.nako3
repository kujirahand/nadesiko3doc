#
# command.txt を読んでWIKIの雛形を生成
#
# パスの解決
ROOT = 母艦パスを「../../」で相対パス展開
NADESIKO3 = ROOT&「/nadesiko3」
NADESIKO3DOC = ROOT&「/nadesiko3doc」
元ファイル = 「{NADESIKO3}/batch/command.txt」
WIKIフォルダ = 「{NADESIKO3DOC}/data」

# ファイルのチェック
もし、元ファイルが存在するならば
　　「読む: command.txt」と表示。
違えば
　　「エラー: 存在しません: {元ファイル}」と表示して終わる。
ここまで。

# 変数の初期化
プラグイン名＝空。
プラグイン拡張=空。
表題＝空。
目次＝「」
SQL=「」
TPL=「」
WIKI=""
PLUG={}
PLUG2={}
PLUG3={}
PLUG拡張={}

プラグイン一覧＝[]
拡張プラグイン辞書={}
表題辞書={}
プラグインページ辞書={}

編集禁止メッセージ=「
#rem(★★★ 編集禁止 ★★★)
#rem(★このファイルはスクリプトで自動で生成されました。)
#rem(★なでしこ=v{ナデシコバージョン})
#rem(★生成={今日} {今})
#rem(★スクリプト=cmd2wiki.nako)
」&改行。
命令一覧＝「
{編集禁止メッセージ}
■ [[命令一覧/機能順]]
なでしこ3の命令一覧です。
」

# メイン処理
元ファイルロード。
プラグイン一覧ページ作成。
命令一覧保存。
プラグインページ辞書保存。
表題辞書保存。
終わる。

●(SをFへ|Fに)WIKI保存とは
　　FF=「{WIKIフォルダ}/{F}」
　　P＝FFからパス抽出。
　　もし！(Pが存在）ならば
　　　　Pにフォルダ作成。
　　ここまで。
　　SをFFへ保存。
　　「保存: {FF}」を表示。
ここまで。

●表題辞書保存とは
　　表題辞書を反復
　　　　表題＝対象キー。
　　　　本文＝対象。
　　　　本文を「{表題}.txt」へWIKI保存。
　　ここまで。
ここまで。

●プラグインページ辞書保存とは
　　プラグイン一覧を反復
　　　　プラグイン名=対象
　　　　S=プラグインページ辞書[対象]
　　　　F=「{プラグイン名}.txt」
　　　　FにSをWIKI保存。
　　　　「プラグインページ: {F}」を表示。
　　ここまで。
ここまで。

●命令一覧保存とは
　　F=「{WIKIフォルダ}/命令一覧/機能順.txt」
　　命令一覧をFへ保存。
　　「命令一覧保存：{F}」を表示。
ここまで。

●元ファイルロードとは
　　プラグイン一覧＝[]
　　元ファイルを開いて、改行で区切る。
　　反復
　　　　対象を「/^■([a-zA-Z0-9\-\_]+)(\(.+\))?/」で正規表現マッチ。
　　　　もし、そうならば
　　　　　　プラグイン名解析。
　　　　　　続ける。
　　　　ここまで。
　　　　対象を「/^●(.+)/」で正規表現マッチ。
　　　　もし、そうならば
　　　　　　プラグイン表題解析。
　　　　　　続ける。
　　　　ここまで。
　　　　対象を「/^\|(.+)/」で正規表現マッチ。
　　　　もし、そうならば
　　　　　　コマンド解析。
　　　　　　続ける。
　　　　ここまで。
　　ここまで。
ここまで。

●プラグイン名解析とは
　　プラグイン名は、抽出文字列[0]
　　もし、抽出文字列[1]ならば
　　　　拡張＝「(拡張プラグイン)」
　　違えば
　　　　拡張＝「」
　　ここまで。
　　拡張プラグイン辞書[プラグイン名] = 拡張。
　　プラグイン一覧にプラグイン名を配列追加。
　　＃命令一覧
　　プラグイン解説＝「」& _
　　　　「●[[{プラグイン名}]]{拡張}」&改行 & _
　　　　「#include({プラグイン名}__解説)」&改行。
　　命令一覧＝命令一覧＆改行＆プラグイン解説。
　　＃プラグインページ辞書
　　プラグインページ辞書[プラグイン名]=プラグイン解説。
ここまで。

●プラグイン表題解析
　　表題は抽出文字列[0]
　　表題ページ名＝「{プラグイン名}/{表題}」
　　表題行＝「▲[[{プラグイン名}]] / [[{表題}:{表題ページ名}]]」
　　命令一覧＝命令一覧＆改行&表題行&改行。
　　表題辞書["{プラグイン名}/{表題}"] = 「●[[{プラグイン名}]]/[[{表題}:{プラグイン名}/{表題}]]」&改行。
　　プラグインページ辞書[プラグイン名]＝プラグインページ辞書[プラグイン名]＆表題行＆改行。
ここまで。

●コマンド解析
　　CMDは抽出文字列[0]
　　CMDを「|」で区切って、Cに代入。
　　Fタイプ＝トリム(C[0])
　　F名前＝トリム(C[1])
　　F引数＝トリム(C[2])の「/」を「|」に置換
　　F説明＝トリム(C[3])
　　Fヨミ＝トリム(C[4])
　　Fカナ1＝LEFT(ヨミ,1)
　　# エラーチェック - 表題とコマンドが同名はエラーに
　　もし、表題＝F名前ならば
　　　　「エラー：{プラグイン名}の『{表題}』とコマンドが同名です。」と表示。
　　　　終わる。
　　ここまで。
　　＃ページ名など
　　ページ名＝「{プラグイン名}/{F名前}」
　　ページ名解説＝「{プラグイン名}/{F名前}__解説」
　　もし、Fタイプ=「関数」ならば
　　　　コマンド関数解析(F名前,F引数,F説明,Fヨミ,Fカナ,ページ名,ページ名解説)。
　　違えば
　　　　コマンド定数解析(F名前,F引数,F説明,Fヨミ,Fカナ,ページ名,ページ名解説)。
　　ここまで。
ここまで。

●コマンド関数解析(F名前,F引数,F説明,Fヨミ,Fカナ1,ページ名,ページ名解説)
　　FF＝「」
　　もし、F引数＝空ならば
　　　　FF=「- [[{F名前}:{ページ名}]] --- {F説明}」&改行
　　違えば
　　　　FF=「- [[({F引数}){F名前}:{ページ名}]] --- {F説明}」&改行
　　ここまで。
　　命令一覧＝命令一覧＆FF。
　　プラグインページ辞書[プラグイン名]＝プラグインページ辞書[プラグイン名]＆FF。
　　表題辞書["{プラグイン名}/{表題}"]＝表題辞書["{プラグイン名}/{表題}"]＆FF。
　　＃WIKI用のテキスト
　　WIKI=編集禁止メッセージ
　　WIKI=WIKI&「#iflogin(▲→編集するには『[[{プラグイン名}/{F名前}__解説]]』へ)」&改行
　　WIKI=WIKI&「■ {F名前} ({Fヨミ})」&改行
　　WIKI=WIKI&『{{{#csv(flag=||)』&改行
　　WIKI=WIKI&「カテゴリ || [[{プラグイン名}]] > [[{表題}:{プラグイン名}/{表題}]]」&改行
　　WIKI=WIKI&「種類 || 関数」&改行
　　WIKI=WIKI&「引数 || {F引数}」&改行
　　WIKI=WIKI&「説明 || {F説明}」&改行
　　WIKI=WIKI&『}}}』&改行&改行
　　WIKI=WIKI&「#include({ページ名解説})」&改行&改行
　　WIKI=WIKI&「▲ 参考」&改行
　　WIKI=WIKI&「- [[命令一覧/機能順]] > [[{プラグイン名}]] > [[{表題}:{プラグイン名}/{表題}]]」&改行
　　WIKI=WIKI&「」&改行
　　WIKIを「{プラグイン名}/{F名前}.txt」へWIKI保存。
　　詳細ページ作成(F名前)
ここまで

●コマンド定数解析(F名前,F引数,F説明,Fヨミ,Fカナ1,ページ名,ページ名解説)
　　FF=「- [[{F名前}:{ページ名}]] -- 定数{改行}」。
　　命令一覧＝命令一覧＆FF。
　　プラグインページ辞書[プラグイン名]＝プラグインページ辞書[プラグイン名]＆FF。
　　表題辞書["{プラグイン名}/{表題}"]＝表題辞書["{プラグイン名}/{表題}"]＆FF。
　　＃WIKI用のテキスト
　　WIKI=編集禁止メッセージ
　　WIKI=WIKI&「#rem(編集するには『{プラグイン名}/{F名前}__解説』を編集します。)」&改行
　　WIKI=WIKI&「■ {F名前} ({Fヨミ})」&改行
　　WIKI=WIKI&「#iflogin([[編集ページへ:{プラグイン名}/{F名前}__解説]])」&改行&改行
　　WIKI=WIKI&『{{{#csv(flag=||)』&改行
　　WIKI=WIKI&「カテゴリ || [[{プラグイン名}]] > [[{表題}:{プラグイン名}/{表題}]]」&改行
　　WIKI=WIKI&「種類 || 定数」&改行
　　WIKI=WIKI&「初期値 || {F説明}」&改行
　　WIKI=WIKI&『}}}』&改行&改行
　　WIKI=WIKI&「#include({ページ名解説})」&改行&改行
　　WIKI=WIKI&「▲ 参考」&改行
　　WIKI=WIKI&「- [[命令一覧/機能順]] > [[{プラグイン名}]]」&改行
　　WIKI=WIKI&「」&改行
　　WIKIを「{プラグイン名}/{F名前}.txt」へWIKI保存。
　　詳細ページ作成(F名前)
ここまで

●詳細ページ作成(F名前)
　　#詳細ページひな形
　　FM=「{WIKIフォルダ}/{プラグイン名}/{F名前}__解説.txt」
　　もし、FMが存在するならば
　　　　T=FMを読む。
　　　　N=Tで「# 準備中」が何文字目。
　　　　もし、N＝0ならば、戻る。
　　ここまで
　　WIKI=「」
　　WIKI=WIKI&「●『[[{F名前}:{プラグイン名}/{F名前}]]』の詳しい解説」&改行&改行
　　WIKI=WIKI&『{{{』&改行
　　WIKI=WIKI&『# 準備中』&改行
　　WIKI=WIKI&『}}}』&改行&改行
　　WIKIをFMへ保存。
　　「ひな形: {FM}」を表示。
ここまで。

●プラグイン一覧ページ作成とは
　　WIKI3=「{編集禁止メッセージ}
■なでしこ3のプラグイン一覧
{改行}{改行}」
　　プラグイン一覧を反復
　　　　プラグイン名＝対象
　　　　拡張=拡張プラグイン辞書[プラグイン名]
　　　　WIKI3=WIKI3&「▲ 🔌[[{プラグイン名}]] {拡張}」&改行
　　　　WIKI3=WIKI3&「#include({プラグイン名}__解説)」&改行&改行
　　ここまで。
　　F＝「{WIKIフォルダ}/プラグイン一覧.txt」
　　FにWIKI3を保存。
　　「プラグイン一覧保存: {F}」を表示。
ここまで。


